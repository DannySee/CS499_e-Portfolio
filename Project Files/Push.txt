Attribute VB_Name = "Push"

'Declare private variables
Private ado As New AdoConnection


'*******************************************************************************
'Executes SQL insert statement to CAL database. Insert deleted customer elements
'into archive table.
'*******************************************************************************
Sub Insert_Deleted(insert_value As String, table As String)

    'Declare sub variables
    Dim cnn As New ADODB.Connection

    'Establish connection to SQL server
    cnn.Open ado.sql_server

    'Insert records into archive table
    cnn.Execute "INSERT INTO " & table & " VALUES(" & insert_value & ")"
End Sub


'*******************************************************************************
'Executes SQL Insert statement to CAL database. Select records from archive
'table and return it to main data table
'*******************************************************************************
Sub Recover_Deleted(obj As Object, primary_key As Long)

    'Declare sub variables
    Dim cnn As New ADODB.Connection
    Dim rst As New ADODB.Recordset
    Dim i As Integer
    Dim fields As String

    'Establish connection to SQL server
    cnn.Open ado.sql_server
        
    'Setup insert fields string
    fields = Replace(obj.AllFlds, "PRIMARY_KEY,", "")

    'Insert records from recovery table to main table
    rst.Open "INSERT INTO " & obj.tbl & " " _
        & "SELECT " & fields & " FROM " & obj.Tbl_Archive & " " _
        & "WHERE PRIMARY_KEY = " & primary_key, cnn
        
    'Remove recovered records from archive
    cnn.Execute ("DELETE FROM " & obj.Tbl_Archive & " WHERE PRIMARY_KEY = " & primary_key)
End Sub


'*******************************************************************************
'Executes SQL Delete statement to CAL database. Returns multidimensional array
'of deleted cusotmer elements.
'*******************************************************************************
Function Get_Deleted(obj As Object, deletion As String) As Variant

    'Declare sub variables
    Dim cnn As New ADODB.Connection
    Dim rst As New ADODB.Recordset
    Dim i As Integer

    'Establish connection to SQL server
    cnn.Open ado.sql_server

    'Loop through each update statement in passthrough array
    rst.Open "DELETE FROM " & obj.Tbl_Archive & " " _
        & "OUTPUT DELETED.* " _
        & "WHERE " & obj.Fld_x & " IN (" & deletion & ")", cnn

    'Return multidimensional array of deleted elements
    If Not rst.EOF Then Get_Deleted = rst.GetRows
End Function
